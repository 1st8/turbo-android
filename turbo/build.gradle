apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'
apply plugin: 'org.jetbrains.dokka'

ext {
    libVersionName = version
    libraryName = 'Turbo Native for Android'
    libraryDescription = 'Android framework for making Turbo native apps'

    bintrayRepo = 'maven'
    bintrayName = 'turbo-android'

    publishedGroupId = 'dev.hotwire'
    publishedArtifactId = 'turbo'

    siteUrl = 'https://github.com/hotwired/turbo-android'
    gitUrl = 'https://github.com/hotwired/turbo-android.git'
}

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5'
        classpath 'org.jetbrains.dokka:dokka-gradle-plugin:1.4.20'
    }
}

repositories {
    google()
    mavenCentral()
    jcenter()
}

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.3'
    testOptions.unitTests.includeAndroidResources = true

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 30

        // Define ProGuard rules for this android library project. These rules will be applied when
        // a consumer of this library sets 'minifyEnabled true'.
        consumerProguardFiles 'proguard-consumer-rules.pro'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        debug.java.srcDirs += 'src/debug/kotlin'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'org.jetbrains.kotlin:kotlin-reflect:1.4.21'
    implementation 'com.google.android.material:material:1.2.1'

    // AndroidX
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

    // JSON
    implementation 'com.google.code.gson:gson:2.8.6'

    // Networking/API
    implementation 'com.squareup.okhttp3:okhttp:4.9.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.1'

    // Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.2'

    // Exported AndroidX dependencies
    api 'androidx.appcompat:appcompat:1.2.0'
    api 'androidx.core:core-ktx:1.3.2'
    api 'androidx.webkit:webkit:1.4.0'
    api 'androidx.navigation:navigation-fragment-ktx:2.3.2'
    api 'androidx.navigation:navigation-ui-ktx:2.3.2'

    // Tests
    testImplementation 'androidx.test:core:1.3.0' // Robolectric
    testImplementation 'androidx.navigation:navigation-testing:2.3.2'
    testImplementation 'android.arch.core:core-testing:1.1.1'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.4.2'
    testImplementation 'org.assertj:assertj-core:3.18.1'
    testImplementation 'org.robolectric:robolectric:4.4'
    testImplementation 'org.mockito:mockito-core:3.6.28'
    testImplementation 'com.nhaarman:mockito-kotlin:1.6.0'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.9.1'
    testImplementation 'junit:junit:4.13.1'
}

// Use the sources jar when publishing
task androidSourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from android.sourceSets.main.java.srcDirs
}

// Publish to GitHub Packages via ./gradlew -Pversion=<version> clean build publish
// https://github.com/orgs/hotwired/packages?repo_name=turbo-android
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // Applies the component for the release build variant
                from components.release

                // Add sources as separate jar
                artifact androidSourcesJar

                // Publication attributes
                groupId = publishedGroupId
                artifactId = publishedArtifactId
                version = libVersionName
            }
        }
        repositories {
            maven {
                name = 'GitHubPackages'
                url = uri('https://maven.pkg.github.com/hotwired/turbo-android')

                credentials {
                    username = System.getenv('GITHUB_ACTOR')
                    password = System.getenv('GITHUB_TOKEN')
                }
            }
        }
    }
}

// Publish to Bintray via ./gradlew -Pversion=<version> clean build bintrayUpload
// https://bintray.com/hotwire/maven/turbo-android
bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['release']
    publish = true
    pkg {
        repo = bintrayRepo
        name = bintrayName
        desc = libraryDescription
        vcsUrl = gitUrl
        version {
            name = libVersionName
            desc = libraryName
            released = new Date()
        }
    }
}

tasks.dokkaJavadoc.configure {
    dokkaSourceSets {
        named("main") {
            includeNonPublic = false
            skipEmptyPackages = true
            skipDeprecated = true
            reportUndocumented = true
            jdkVersion = 8
        }
    }
}
